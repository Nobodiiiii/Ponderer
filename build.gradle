plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0, 6.2)'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
    id 'org.parchmentmc.librarian.forgegradle' version "${librarian_version}"
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" }
    maven { url = "https://maven.createmod.net" }
    maven { url = "https://maven.blamejared.com" }
}

base {
    archivesName = "${mod_id}-${minecraft_version}-forge"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

// Ensure UTF-8 encoding for source files
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

// -- Mixin Support
mixin {
    add(sourceSets.main, "${mod_id}.refmap.json")
    config("${mod_id}.mixins.json")
}

minecraft {
    if (Boolean.parseBoolean(project.use_parchment)) {
        mappings channel: "parchment", version: "${parchment_version}-${minecraft_version}"
    } else {
        mappings channel: "official", version: "${minecraft_version}"
    }

    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file("run")

            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"

            property "mixin.env.remapRefMap", "true"
            property "mixin.env.refMapRemappingFile", "${projectDir}/build/createSrgToMcp/output.srg"

            arg "-mixin.config=${mod_id}.mixins.json"

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            taskName "Client"
        }

        server {
            taskName "Server"
            args '--nogui'
        }

        data {
            taskName "Data"
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft("net.minecraftforge:forge:${minecraft_version}-${forge_version}")

    // Ponder - full mod, available at runtime for dev testing
    implementation(fg.deobf("net.createmod.ponder:Ponder-Forge-${minecraft_version}:${ponder_version}"))
    // Flywheel - required by Ponder at runtime
    implementation(fg.deobf("dev.engine-room.flywheel:flywheel-forge-${minecraft_version}:${flywheel_version}"))
    // Flywheel API - for compilation
    compileOnly(fg.deobf("dev.engine-room.flywheel:flywheel-forge-api-${minecraft_version}:${flywheel_version}"))

    // JEI - compile API + full mod at runtime for dev testing
    compileOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"))
    compileOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"))
    runtimeOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}"))

    // Mixin annotation processor
    annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")
}

// This block of code expands all declared replace properties in the specified resource targets.
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [minecraft_version      : minecraft_version,
                             minecraft_version_range: minecraft_version_range,
                             forge_version_range    : forge_version_range,
                             loader_version_range   : loader_version_range,
                             mod_id                 : mod_id,
                             mod_name               : mod_name,
                             mod_license            : mod_license,
                             mod_version            : mod_version,
                             mod_authors            : mod_authors,
                             mod_description        : mod_description]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources.srcDir generateModMetadata

jar.finalizedBy("reobfJar")

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

// IntelliJ Mixin annotation processor workaround
if (System.getProperty("idea.sync.active") == "true") {
    afterEvaluate {
        tasks.withType(JavaCompile).all {
            it.options.annotationProcessorPath = files()
        }
    }
}
